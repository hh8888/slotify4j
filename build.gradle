apply plugin: "java"
apply plugin: "jacoco"
apply plugin: "com.jfrog.bintray"
apply plugin: "maven-publish"

version "1.0.0"
group "com.github.sta-ger"

repositories {
    mavenCentral()
}

dependencies {
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.4.2")
    testRuntime("org.junit.jupiter:junit-jupiter-engine:5.4.2")
}

jacocoTestReport {
    reports {
        xml.enabled true
    }
}

test {
    useJUnitPlatform()
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+"
    }
}

def pomConfig = {
    developers {
        developer {
            name "sta-ger"
            email "pascaldelger@gmail.com"
        }
    }

    scm {
        url "https://github.com/sta-ger/slotify4j"
        connection "https://git@github.com/sta-ger/slotify4j"
        developerConnection "scm:git:https://github.com/sta-ger/slotify4j"
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

artifacts {
    archives sourcesJar
}

publishing {
    publications {
        MavenCustom(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier "sources"
            }
            groupId project.group
            artifactId project.name
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode("description", "A server-side video slot game logic framework for Java.")
                root.appendNode("name", "slotify4j")
                root.appendNode("url", "https://github.com/sta-ger/slotify4j")
                root.children().last() + pomConfig
            }
        }
    }
}

bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_API_KEY")
    dryRun = false
    override = false
    publish = true
    pkg {
        repo = "maven"
        name = project.name
        userOrg = "sta-ger"
        licenses = ["Apache-2.0"]
        desc = "A server-side video slot game logic framework for Java."
        vcsUrl = "https://github.com/sta-ger/slotify4j.git"

        version {
            name = project.version
        }
    }

    publications = ["MavenCustom"]
}

check.dependsOn jacocoTestReport
